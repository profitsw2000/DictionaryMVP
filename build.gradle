// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.0' apply false
}

//For all subprojects - generate compileSdkVersion, minSdkVersion, targetSdkVersion and Java compatibility
subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android') ?: project.plugins.findPlugin('android-library')) {
            android {
                compileSdk Config.compile_sdk

                buildFeatures {
                    viewBinding true
                }

                defaultConfig {
                    minSdkVersion Config.min_sdk
                    targetSdkVersion Config.target_sdk
                }

                compileOptions {
                    sourceCompatibility Config.java_version
                    targetCompatibility Config.java_version
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}